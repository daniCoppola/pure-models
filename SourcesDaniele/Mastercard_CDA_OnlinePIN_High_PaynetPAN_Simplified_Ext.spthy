theory Mastercard_CDA_OnlinePIN_High_PaynetPAN

begin

builtins:   signing, hashing, asymmetric-encryption, xor, diffie-hellman

functions:  NEq/2,
            f/2,kdf/2,
            MAC/2, MAC_arpc/2,
            p8/1

rule Terminal_Bank_Network:
    [ Send(S, R, channelID, msg) ]-->[ Recv(S, R, channelID, msg) ]

rule Generate_Amount_Low:
    [ ]--[ Once(<$amount, 'Amount'>) ]->[ !Value($amount, 'Low') ]

rule Generate_Amount_High:
    [ ]--[ Once(<$amount, 'Amount'>) ]->[ !Value($amount, 'High') ]

rule Generate_ATC:
    [ Fr(~ATC) ]-->[ !ATC(~ATC), Out(~ATC) ]

rule Generate_SDAD_Format_Visa:
    [ ]--> [ !SDADFormat('05', 'TC'), !SDADFormat('95', 'ARQC') ]

rule Create_CA:
    let pubkCA = pk(~privkCA)
        cont = <'01', $CA, pubkCA, $CA>
        cert = <cont, sign{cont}~privkCA>
    in
    [ Fr(~privkCA) ]
  --[ Once($CA),
      Role($CA, 'CA') ]->
    [ !LtkCA($CA, ~privkCA),
      !CertCA($CA, cert),
      Out(pubkCA)
    ]

rule Create_Bank:
    let pubkBank = pk(~privkBank)
        cont = <'02', $Bank, pubkBank, $CA>
        cert = <cont, sign{cont}~privkCA>
    in
    [ Fr(~privkBank),
      !LtkCA($CA, ~privkCA) ]
  --[ Once($Bank),
      Role($Bank, 'Bank') ]->
    [ !LtkBank($Bank, ~privkBank),
      !CertBank($Bank, cert),
      !IssuingCA($Bank, $CA),
      Out(pubkBank)
    ]

rule Compromise_CA:
    [ !LtkCA($CA, ~privkCA) ]
  --[ Compromise($CA) ]->
    [ Out(~privkCA)]

rule Compromise_Bank:
    [ !LtkBank($Bank, ~privkBank) ]
  --[ Compromise($Bank) ]->
    [ Out(<$Bank, ~privkBank>) ]

rule Compromise_Card:
    [ !LtkCard(~PAN, ~privkCard) ]
  --[ Compromise(~PAN) ]->
    [ Out(<~PAN, ~privkCard>) ]

rule Compromise_Bank_Card_ShK:
    [ !IssuingBank(~PAN, $Bank),
      !Shk(~PAN, ~MK) ]
  --[ Compromise($Bank), Compromise(~PAN) ]->
    [ Out(~MK) ]

rule Set_PIN:
    [ Fr(~PIN),
      Set_PIN(~PAN, CVM, $CA, $Bank) ]
  --[ NEq(CVM, 'NoPIN'),
      SecretPIN(~PIN),
      Honest($CA), Honest($Bank), Honest(~PAN) ]->
    [ !PIN(~PAN, ~PIN),
      !Entered_PIN(~PAN, ~PIN),
      !Entered_PIN(~PAN, 'WrongPIN')
    ]

rule Create_Card:
    let AIP = <auth, $furtherData>
        SSAD = sign{'03', ~PAN, ~expDate, AIP}~privkBank
    in
    [ Fr(~PAN),
      Fr(~expDate),
      Fr(~MK),
      !LtkBank($Bank, ~privkBank),
      !CertBank($Bank, certBank),
      !IssuingCA($Bank, $CA),
      In(<auth, CVM>)
    ]
  --[ Role(~PAN, 'Card'),
      SecretPAN(~PAN),
      SecretMK(~MK),
      Honest($CA), Honest($Bank), Honest(~PAN) ]->
    [ !AIP(~PAN, AIP),
      !AID(~PAN, 'Mastercard'),
      !Shk(~PAN, ~MK),
      !IssuingBank(~PAN, $Bank),
      Set_Records(~PAN, ~expDate, $CA, certBank, SSAD, CVM),
      Set_PIN(~PAN, CVM, $CA, $Bank) ]

rule Set_Records_SDA:
    [ Set_Records(~PAN, ~expDate, $CA, certBank, SSAD, CVM),
      !AIP(~PAN, <'SDA', furtherData>) ]
  -->
    [ !Records(~PAN, <~PAN, ~expDate, $CA, certBank, SSAD, CVM>) ]

rule Set_Records_NotSDA:
    let pubkCard = pk(~privkCard)
        cont = <'04', ~PAN, pubkCard, $Bank, CVM, AIP>
        certCard = <cont, sign{cont}~privkBank>
    in
    [ Set_Records(~PAN, ~expDate, $CA, certBank, SSAD, CVM),
      Fr(~privkCard),
      !AIP(~PAN, AIP),
      !IssuingBank(~PAN, $Bank),
      !LtkBank($Bank, ~privkBank) ]
  --[ NEq(fst(AIP), 'SDA'),
      SecretPrivkCard(~privkCard),
      Honest($CA), Honest($Bank), Honest(~PAN) ]->
    [ Out(pubkCard),
      !LtkCard(~PAN, ~privkCard),
      !Records(~PAN, <~PAN, ~expDate, $CA, certBank, certCard, CVM>) ]

rule Terminal_Sends_GPO:
    let date = 'YYMMDD'
        type = 'Purchase'
        currency = 'CHF'
        country = 'Switzerland'
        PDOL = <$amount, country, currency, date, type, ~UN>
    in
    [ Fr(~UN),
      !Value($amount, value) ]
  --[ OneTerminal(),
      Role($Terminal, 'Terminal') ]->
    [ Out(<'GET_PROCESSING_OPTIONS', PDOL>),
      Terminal_Sent_GPO($Terminal, PDOL) ]

rule Card_Responds_To_GPO:
    [ In(<'GET_PROCESSING_OPTIONS', PDOL>),
      !AIP(~PAN, AIP),
      !AID(~PAN, 'Mastercard'),
      !ATC(ATC) ]
  --[ OneCard(),
      Once(<~PAN, ATC, 'Card'>) ]->
    [ Out(<AIP, 'AFL'>),
      Card_Responded_To_GPO(~PAN, PDOL, ATC)]

// BEGIN ADDITIONAL RULES FOR DH EXCHANGE

/*rule Compromise_Ephemeral_Key_Terminal:
	[ Ephemeral_Key_Terminal($Terminal, Eph_Pk, ~Eph_Sk)]
	--[Compromise(<$Terminal, Eph_Pk>)]->
	[Out(<Eph_Pk, ~Eph_Sk>)]

rule Compromise_Ephemeral_Key_Card:
	[ Ephemeral_Key_Card(~PAN, Eph_Pk, ~Eph_Sk)]
	--[Compromise(<~PAN, Eph_Pk>)]->
	[Out(<Eph_Pk, ~Eph_Sk>)]*/

/* 
    Generates an epheremal DH value and sends it to the card. The state 
    is saved in Terminal_Ext_Sent_Hello.
*/
rule Terminal_Ext_Sends_Hello:
let
		Terminal_Eph_Pk = 'g'^~Terminal_Eph_Sk
	in 
	[ 
		Terminal_Sent_GPO($Terminal, PDOL), In(<AIP, 'AFL'>), Fr(~Terminal_Eph_Sk)
	] -->
	[	Terminal_Ext_Sent_Hello($Terminal, PDOL, AIP, < Terminal_Eph_Pk, ~Terminal_Eph_Sk>) , Out(<'EXT_HELLO', Terminal_Eph_Pk>)
  //,Ephemeral_Key_Terminal($Terminal, Terminal_Eph_Pk, ~Terminal_Eph_Sk)
  ]

/*
    Creates an ephemeral DH value, derives a shared secret with the terminal. 
    Computes a MAC over the EDH values and sends the MAC and EDH to the terminal.
*/
rule Card_Ext_Responds_To_Hello:
	let 
		Card_Eph_Pk = 'g'^~Card_Eph_Sk
		DH = <Card_Eph_Pk, Terminal_Eph_Pk, Terminal_Eph_Pk^~Card_Eph_Sk>
		mac = MAC(f(Terminal_Eph_Pk^~Card_Eph_Sk,'MAC_CARD_TO_READER'), <Card_Eph_Pk, Terminal_Eph_Pk>)
		EXT_HELLO = <'EXT_HELLO', Terminal_Eph_Pk>
	in
	[	In(EXT_HELLO),
		Card_Responded_To_GPO(~PAN, PDOL, ATC),
		Fr(~Card_Eph_Sk)
	]-->
	[
		Out(<'EXT_HELLO', Card_Eph_Pk, mac>),
		Card_Responded_To_Hello(~PAN, PDOL, ATC, DH) 
    //,Ephemeral_Key_Card(~PAN, Card_Eph_Pk, ~Card_Eph_Sk)
	]

/*
    Derives a shared secret with the card given the EDH save in the state
    and the EDH received from the card.
*/
rule Terminal_Ext_Receives_Hello:
	let
	mac_t = MAC(f(Card_Eph_Pk^~Terminal_Eph_Sk,'MAC_CARD_TO_READER'), <Card_Eph_Pk, Terminal_Eph_Pk>)
		edht = <Terminal_Eph_Pk, ~Terminal_Eph_Sk>
		DH = <Card_Eph_Pk, Terminal_Eph_Pk, Card_Eph_Pk^~Terminal_Eph_Sk>
		EXT_HELLO = <'EXT_HELLO', Card_Eph_Pk, mac_c>
	in
	[	
		Terminal_Ext_Sent_Hello($Terminal, PDOL, AIP, edht),
		In(EXT_HELLO)
	]--[Eq(mac_c, mac_t), Terminal_Ext_Sent_Hello_dbg($Terminal, PDOL, AIP, edht) ]->

	[
		Terminal_Ext_Received_Hello($Terminal, PDOL, AIP, DH)
	]

// END ADDITIONAL RULES FOR DH EXCHANGE 

rule Terminal_Sends_ReadRecord:
    [ Terminal_Ext_Received_Hello($Terminal, PDOL, AIP, DH)]
  -->
    [ Out(<'READ_RECORD', 'AFL'>),
      Terminal_Sent_ReadRecord($Terminal, PDOL, AIP, DH) ]

rule Card_Responds_To_ReadRecord_NotDDA:
    [ Card_Responded_To_Hello(~PAN, PDOL, ATC, DH),
      !AIP(~PAN, AIP),
      !Records(~PAN, records),
      In(<'READ_RECORD', 'AFL'>) ]
  --[ NEq(fst(AIP), 'DDA') ]->
    [ Out(records),
      Card_Ready_For_Cryptogram(~PAN, PDOL, ATC, DH) ]

/*rule Card_Responds_To_ReadRecord_DDA:
    [ Card_Responded_To_GPO(~PAN, PDOL, ATC, DH),
      !Records(~PAN, records),
      !AIP(~PAN, <'DDA', furtherData>),
      In(<'READ_RECORD', 'AFL'>) ]
  -->
    [ Out(records),
      Card_Ready_For_DDA(~PAN, PDOL, ATC, DH) ]*/

/*rule Terminal_Receives_Records_SDA:
    let AIP = <'SDA', furtherData>
        records = <~PAN, expDate, $CA,
                   <<'02', $Bank, pubkBank, $CA>, sign2>,
                   SSAD, CVM>
    in
    [ Terminal_Sent_ReadRecord($Terminal, PDOL, AIP, DH),
      In(records),
      !IssuingCA($Bank, $CA),
      !CertCA($CA, <<'01', $CA, pubkCA, $CA>, sign1>) ]
  --[
      Eq( verify(sign1, <'01', $CA, pubkCA, $CA>, pubkCA), true),
      Eq( verify(sign2, <'02', $Bank, pubkBank, $CA>, pubkCA), true),
      Eq( verify(SSAD, <'03', ~PAN, expDate, AIP>, pubkBank), true)
    ]->
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, 'Null', CVM) ]*/

rule Terminal_Receives_Records_CDA:
    let AIP = <'CDA', furtherData>
        records = <~PAN, expDate, $CA,
                   <<'02', $Bank, pubkBank, $CA>, sign2>,
                   <<'04', ~PAN, pubkCard, $Bank, CVM, AIP>, sign3>,
                   CVM>
    in
    [ Terminal_Sent_ReadRecord($Terminal, PDOL, AIP, DH),
      In(records),
      !IssuingCA($Bank, $CA),
      !CertCA($CA, <<'01', $CA, pubkCA, $CA>, sign1>) ]
  --[
      Eq( verify(sign1, <'01', $CA, pubkCA, $CA>, pubkCA), true),
      Eq( verify(sign2, <'02', $Bank, pubkBank, $CA>, pubkCA), true),
      Eq( verify(sign3, <'04', ~PAN, pubkCard, $Bank, CVM, AIP>, pubkBank), true)
    ]->
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM) ]

/*rule Terminal_Receives_Records_DDA:
    let AIP = <'DDA', furtherData>
        records = <~PAN, expDate, $CA,
                   <<'02', $Bank, pubkBank, $CA>, sign2>,
                   <<'04', ~PAN, pubkCard, $Bank, CVM, AIP>, sign3>,
                   CVM>
    in
    [ Terminal_Sent_ReadRecord($Terminal, PDOL, AIP, DH),
      !IssuingCA($Bank, $CA),
      In(records),
      !CertCA($CA, <<'01', $CA, pubkCA, $CA>, sign1>) ]
  --[
      Eq( verify(sign1, <'01', $CA, pubkCA, $CA>, pubkCA), true),
      Eq( verify(sign2, <'02', $Bank, pubkBank, $CA>, pubkCA), true),
      Eq( verify(sign3,<'04', ~PAN, pubkCard, $Bank, CVM, AIP>, pubkBank), true)
    ]->
    [ Terminal_Ready_For_DDA($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM) ]

rule Terminal_Sends_InternalAuthenticate:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        DDOL = ~UN
    in
    [ Terminal_Ready_For_DDA($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM) ]
  -->
    [ Out( <'INTERNAL_AUTHENTICATE', DDOL> ),
      Terminal_Sent_InternalAuthenticate($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM) ]

rule Card_Responds_To_InternalAuthenticate:
    let d = <'05', ~nc, DDOL>
    in
    [ Card_Ready_For_DDA(~PAN, PDOL, ATC, DH),
      Fr(~nc),
      !LtkCard(~PAN, ~privkCard),
      In( <'INTERNAL_AUTHENTICATE', DDOL> ) ]
  -->
    [ Out( <~nc, sign{d}~privkCard> ),
      Card_Ready_For_Cryptogram(~PAN, PDOL, ATC, DH) ]

rule Terminal_Receives_InternalAuthenticate_Response:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        d = <'05', nc, ~UN>
    in
    [ Terminal_Sent_InternalAuthenticate($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, CVM),
      In(<nc, SDAD>) ]
  --[
      Eq( verify(SDAD, d, pubkCard), true) ]->
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM) ]*/

rule Terminal_Processes_CVM_NoPIN:
    let PDOL = <$amount, country, currency, date, type, ~UN>
    in
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, CVM),
      !Value($amount, 'Low') ]
  -->
    [ Terminal_Ready_For_Cryptogram($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, 'NoPIN', 'Null', CVM) ]

rule Terminal_Processes_CVM_OnlinePIN:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        encPIN = aenc{PIN}pubkBank
    in
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA, PDOL, 
        AIP, DH, pubkBank, pubkCard, 'OnlinePIN'),
      !Entered_PIN(~PAN, PIN),
      !Value($amount, 'High') ]
  -->
    [ Terminal_Ready_For_Cryptogram($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, 'OnlinePIN', encPIN, 'OnlinePIN') ]

rule Terminal_Processes_CVM_ODCVM:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        AIP = <auth, <'ODCVM', furtherData2>>
    in
    [ Terminal_Ready_For_CVM($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, CVM),
      !Value($amount, 'High') ]
  -->
    [ Terminal_Ready_For_Cryptogram($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, 'ODCVM', 'Null', CVM) ]

/*rule Terminal_Sends_GenerateAC_NoCDA:
    let CDOL1 = <'TVR', CVM, 'HHMMSS'>
        X = <PDOL, CDOL1>
    in
    [ Terminal_Ready_For_Cryptogram($Terminal, ~PAN, $Bank, $CA, PDOL,
        AIP, DH, pubkBank, pubkCard, CVM, encPIN, supportedCVM),
      In(acType) ]
  --[ NEq(fst(AIP), 'CDA') ]->
    [ Out(< 'GENERATE_AC', acType, 'NoCDA', CDOL1 >),
      Terminal_Sent_GenerateAC_NoCDA($Terminal, ~PAN, $Bank, $CA,
        X, AIP, pubkBank, pubkCard, CVM, encPIN, acType, supportedCVM) ]*/

rule Terminal_Sends_GenerateAC_CDA:
    let CDOL1 = <'TVR', CVM, 'HHMMSS'>
        X = <PDOL, CDOL1>
        AIP = <'CDA', furtherData>
    in
    [ Terminal_Ready_For_Cryptogram($Terminal, ~PAN, $Bank, $CA,
        PDOL, AIP, DH, pubkBank, pubkCard, CVM, encPIN, supportedCVM),
      In(acType) ]
  -->
    [ Out(< 'GENERATE_AC', acType, 'CDA', CDOL1 >),
      Terminal_Sent_GenerateAC_CDA($Terminal, ~PAN, $Bank, $CA,
        X, AIP, DH, pubkBank, pubkCard, CVM, encPIN, acType, supportedCVM) ]

/*rule Card_Responds_To_GenerateAC_NoCDA:
    let CDOL1 = <'TVR', CVM, 'HHMMSS'>
        X = <PDOL, CDOL1>
        IAD = <'IAD', CID>
        AC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
        transaction = <~PAN, AIP, CVM, X, ,ATC, AC, IAD, 'Mastercard'>
    in
    [ Card_Ready_For_Cryptogram(~PAN, PDOL, ATC, DH),
      !AIP(~PAN, AIP),
      !Shk(~PAN, ~MK),
      !IssuingBank(~PAN, $Bank),
      In(< 'GENERATE_AC', CID, 'NoCDA', CDOL1 >) ]
  --[
      Running(~PAN, 'Terminal', <'Card', 'Terminal', transaction>),
      Running(~PAN, $Bank, <'Card', 'Bank', transaction>) ]->
    [ Out(<CID, ATC, AC, IAD>) ]*/

rule Card_Responds_To_GenerateAC_CDA:
    let PDOL = <amount, country, currency, date, type, UN>
        CDOL1 = <'TVR', CVM, 'HHMMSS'>
        X = <PDOL, CDOL1>
  		  DH = <Card_Eph_Pk, Terminal_Eph_Pk, Secret>
        DH_AUTH = <Card_Eph_Pk, Terminal_Eph_Pk>
        IAD = <'IAD', CID>
        AIP = <'CDA', furtherData>
        AC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
        T = h(<X, CID, ATC, AC, IAD, DH_AUTH>)
        SDAD = sign{'05', ~nc, CID, AC, T, UN}~privkCard
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Mastercard'>
    in
    [ Card_Ready_For_Cryptogram(~PAN, PDOL, ATC, DH),
      !LtkCard(~PAN, ~privkCard),
      !AIP(~PAN, AIP),
      !Shk(~PAN, ~MK),
      !IssuingBank(~PAN, $Bank),
      Fr(~nc),
      In(< 'GENERATE_AC', CID, 'CDA', CDOL1 >) ]
  --[ Running(~PAN, 'Terminal', <'Card', 'Terminal', transaction>),
      Running_DH(~PAN, 'Terminal', DH),
      Running(~PAN, $Bank, <'Card', 'Bank', transaction>) ]->
    [ Out(<CID, ATC, AC, <~nc, SDAD>, IAD>) ]

/*rule Terminal_Receives_AC_NoCDA:
    let transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Mastercard'>
    in
    [ Terminal_Sent_GenerateAC_NoCDA($Terminal, ~PAN, $Bank, $CA,
        X, AIP, pubkBank, pubkCard, CVM, encPIN, acType, supportedCVM),
      In(<CID, ATC, AC, IAD>),
      Fr(~channelID) ]
  --[ Compatible_CID_acType(CID, acType),
      Compatible_CID_CVM(CID, CVM),
      Running($Terminal, $Bank, <'Terminal', 'Bank', transaction>) ]->
    [ Terminal_Received_AC($Terminal, $Bank, $CA,
        'Null', CID, transaction, supportedCVM, ~channelID),
      Send($Terminal, $Bank, <~channelID, '1'>,
        <transaction, encPIN>) ]*/

rule Terminal_Receives_AC_CDA:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        X = <PDOL, CDOL1>
  		  DH = <Card_Eph_Pk, Terminal_Eph_Pk, Secret>
        DH_AUTH = <Card_Eph_Pk, Terminal_Eph_Pk>
        T = h(<X, CID, ATC, AC, IAD, DH_AUTH>)
        d = <'05', nc, CID, AC, T, ~UN>
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Mastercard'>
    in
    [ Terminal_Sent_GenerateAC_CDA($Terminal, ~PAN, $Bank, $CA,
        X, AIP, DH, pubkBank, pubkCard, CVM, encPIN, acType, supportedCVM),
      In(<CID, ATC, AC, <nc, SDAD>, IAD>),
      Fr(~channelID) ]
  --[ Compatible_CID_acType(CID, acType),
      Compatible_CID_CVM(CID, CVM),
      Eq( verify(SDAD, d, pubkCard), true ),
      Running($Terminal, $Bank, <'Terminal', 'Bank', transaction>) ]->
    [ Terminal_Received_AC($Terminal, $Bank, $CA,
        'Null', CID, transaction, supportedCVM, ~channelID),
      Send($Terminal, $Bank, <~channelID, '1'>,
        <transaction, encPIN>) ]

rule Terminal_Commits_TC:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        X = <PDOL, CDOL1>
        AIP = <'CDA', furtherData>
        supportedCVM = 'OnlinePIN'
        value = 'High'
    		DH = <Card_Eph_Pk, Terminal_Eph_Pk, Secret>
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Mastercard'>
    in
    [ Terminal_Received_AC($Terminal, $Bank, $CA,
        nc, 'TC', transaction, supportedCVM, ~channelID),
      !Value($amount, value) ]
  --[ Ignore(),
      TerminalAccepts(transaction),
      Commit('Terminal', ~PAN, <'Card', 'Terminal', transaction>),
      Secret_Sigma(Secret),
      Commit_DH('Terminal', ~PAN, DH),
      Honest($CA), Honest($Bank), Honest($Terminal), Honest(~PAN),
      Honest(<~PAN, Card_Eph_Pk>), Honest(<$Terminal, Terminal_Eph_Pk>)]->
    [ ]

rule Terminal_Commits_ARQC:
    let PDOL = <$amount, country, currency, date, type, ~UN>
        X = <PDOL, CDOL1>
        AIP = <'CDA', furtherData>
        supportedCVM = 'OnlinePIN'
        value = 'High'
    		DH = <Card_Eph_Pk, Terminal_Eph_Pk, Secret>
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Mastercard'>
    in
    [ Terminal_Received_AC($Terminal, $Bank, $CA,
        nc, 'ARQC', transaction, supportedCVM, ~channelID),
      !Value($amount, value),
      Recv($Bank, $Terminal, <~channelID, '2'>, <'ARC', ARPC>) ]
  --[ TerminalAccepts(transaction),
      Commit('Terminal', ~PAN, <'Card', 'Terminal', transaction>),
      Commit($Terminal, $Bank, <'Bank', 'Terminal', transaction>),
      Secret_Sigma(Secret),
      Commit_DH('Terminal', ~PAN, DH),
      Honest($CA), Honest($Bank), Honest($Terminal), Honest(~PAN),
      Honest(<~PAN, Card_Eph_Pk>), Honest(<$Terminal, Terminal_Eph_Pk>) ]->
    [ ]
/*
rule Create_Card_Visa:
    let pubkCard = pk(~privkCard)
        cont = <'04', ~PAN, pubkCard, $Bank>
        certCard = <cont, sign{cont}~privkBank>
    in
    [ Fr(~PAN),
      Fr(~expDate),
      Fr(~privkCard),
      Fr(~MK),
      !LtkBank($Bank, ~privkBank),
      !CertBank($Bank, certBank),
      !IssuingCA($Bank, $CA)
    ]
  --[ Role(~PAN, 'Card'),
      SecretPAN(~PAN),
      SecretMK(~MK),
      SecretPrivkCard(~privkCard),
      Honest($CA), Honest($Bank), Honest(~PAN) ]->
    [ !LtkCard(~PAN, ~privkCard),
      !AID(~PAN, 'Visa'),
      Out(pubkCard),
      !ExpirationDate(~PAN, ~expDate),
      !Shk(~PAN, ~MK),
      !IssuingBank(~PAN, $Bank),
      !Records_Visa(~PAN, ~expDate, $CA, certBank, certCard),
      Set_PIN(~PAN, 'OnlinePIN', $CA, $Bank) ]

rule Terminal_Sends_GPO_Low_Visa:
    let date = 'YYMMDD'
        type = 'Purchase'
        currency = 'CHF'
        country = 'Switzerland'
        TTQ = <'TC', 'NoPIN'>
        PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
    in
    [ Fr(~UN),
      !Value($amount, 'Low') ]
  --[ OneTerminal(),
      Role($Terminal, 'Terminal') ]->
    [ Out(<'GET_PROCESSING_OPTIONS', PDOL>),
      Terminal_Sent_GPO_Visa($Terminal, PDOL) ]

rule Terminal_Sends_GPO_High_Visa:
    let date = 'YYMMDD'
        type = 'Purchase'
        currency = 'CHF'
        country = 'Switzerland'
        TTQ = <'ARQC', 'OnlinePIN'>
        PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
    in
    [ Fr(~UN),
      !Value($amount, 'High') ]
  --[ OneTerminal(),
      Role($Terminal, 'Terminal') ]->
    [ Out(<'GET_PROCESSING_OPTIONS', PDOL>),
      Terminal_Sent_GPO_Visa($Terminal, PDOL) ]

rule Card_Responds_To_GPO_EMV_Visa:
    let PDOL = <<acType, CVM>, amount, country, currency, date, type, UN>
        track2ED = <~PAN, ~expDate>
        CID = 'ARQC'
        CTQ = CVM
        IAD = <'IAD', CID>
        AIP = <$furtherData, 'EMV'>
        CDOL1 = <'TVR', 'NoPIN', 'HHMMSS'>
        X = <<amount, country, currency, date, type, UN>, CDOL1>
        AC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
        transaction = <~PAN, AIP, CTQ, X, ATC, DH, AC, IAD, 'Visa'>
    in
    [ In(<'GET_PROCESSING_OPTIONS', PDOL>),
      !Shk(~PAN, ~MK),
      !AID(~PAN, 'Visa'),
      !ExpirationDate(~PAN, ~expDate),
      !IssuingBank(~PAN, $Bank),
      !ATC(ATC) ]
  --[ OneCard(),
      Once(<~PAN, ATC, 'Card'>),
      NEq(CVM, 'CDCVM'),
      Running(~PAN, 'Terminal', <'Card', 'Terminal', transaction>),
      Running(~PAN, $Bank, <'Card', 'Bank', transaction>) ]->
    [ Out(< AIP, 'AFL', track2ED, IAD, AC, CID, ATC, CTQ >),
      Card_Responded_To_GPO_Visa(~PAN, AIP, PDOL, ATC, DH, AC, CID, CTQ, IAD) ]

rule Card_Responds_To_GPO_DDA_Visa:
    let PDOL = <<CID, CVM>, amount, country, currency, date, type, UN>
        track2ED = <~PAN, ~expDate>
        CTQ = CVM
        IAD = <'IAD', CID>
        AIP = <$furtherData, 'DDA'>
        CDOL1 = <'TVR', 'NoPIN', 'HHMMSS'>
        X = <<amount, country, currency, date, type, UN>, CDOL1>
        AC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
    in
    [ In(<'GET_PROCESSING_OPTIONS', PDOL>),
      !Shk(~PAN, ~MK),
      !AID(~PAN, 'Visa'),
      !ExpirationDate(~PAN, ~expDate),
      !ATC(ATC) ]
  --[ OneCard(),
      Once(<~PAN, ATC, 'Card'>),
      NEq(CVM, 'CDCVM')
    ]->
    [ Out(< AIP, 'AFL', track2ED, IAD, AC, CID, ATC, CTQ >),
      Card_Responded_To_GPO_Visa(~PAN, AIP, PDOL, ATC, DH, AC, CID, CTQ, IAD) ]

rule Terminal_Sends_ReadRecord_Visa:
    let PDOL = <<acType, CVM>, $amount, country, currency, date, type, ~UN>
    in
    [ Terminal_Sent_GPO_Visa($Terminal, PDOL),
      In(< AIP, 'AFL', <~PAN, expDate>, <'IAD', CID>, AC, CID, ATC, CTQ >) ]
  --[ Compatible_CID_acType(CID, acType) ]->
    [ Out(< 'READ_RECORD', 'AFL' >),
      Terminal_Sent_ReadRecord_Visa($Terminal, PDOL, AIP, DH, ~PAN,
        expDate, <'IAD', CID>, AC, CID, ATC, CTQ) ]

rule Card_Responds_To_ReadRecord_EMV_Visa:
    let AIP = <furtherData, 'EMV'>
        CDOL1 = <'TVR', 'NoPIN', 'HHMMSS'>
        X = <<amount, country, currency, date, type, UN>, CDOL1>
        transaction = <~PAN, AIP, CTQ, X, ATC, DH, AC, IAD, 'Visa'>
    in
    [ Card_Responded_To_GPO_Visa(~PAN, AIP, PDOL, ATC, DH, AC, CID, CTQ, IAD),
      !Records_Visa(~PAN, ~expDate, $CA, certBank, certCard),
      In(< 'READ_RECORD', 'AFL' >) ]
  -->
    [ Out(<~PAN, ~expDate>) ]

rule Card_Responds_To_ReadRecord_DDA_Visa:
    let PDOL = <TTQ, amount, country, currency, date, type, UN>
        AIP = <furtherData, 'DDA'>
        d = <format, ATC, UN, amount, 'CHF', ~nc, CTQ, AIP>
        SDAD = sign{d}~privkCard
        CDOL1 = <'TVR', 'NoPIN', 'HHMMSS'>
        X = <<amount, country, currency, date, type, UN>, CDOL1>
        transaction = <~PAN, AIP, CTQ, X, ATC, DH, AC, IAD, 'Visa'>
    in
    [ Card_Responded_To_GPO_Visa(~PAN, AIP, PDOL, ATC, DH, AC, CID, CTQ, IAD),
      !Records_Visa(~PAN, ~expDate, $CA, certBank, certCard),
      Fr(~nc),
      !LtkCard(~PAN, ~privkCard),
      !SDADFormat(format, CID),
      !IssuingBank(~PAN, $Bank),
      In(< 'READ_RECORD', 'AFL' >) ]
  --[ Running(~PAN, 'Terminal', <'Card', 'Terminal', transaction>),
      Running(~PAN, $Bank, <'Card', 'Bank', transaction>) ]->
    [ Out(< ~PAN, ~expDate, $CA, certBank, certCard, ~nc, CTQ, SDAD >) ]

rule Terminal_Receives_Records_EMV_Visa:
    let AIP = <furtherData, 'EMV'>
        records = <~PAN, expDate>
    in
    [ Terminal_Sent_ReadRecord_Visa($Terminal, PDOL, AIP, DH, ~PAN,
        expDate, IAD, AC, 'ARQC', ATC, CTQ),
      In(records),
      !IssuingBank(~PAN, $Bank),
      !CertBank($Bank, <<'02', $Bank, pubkBank, $CA>, sign2>) ]
  --[ Ignore() ]->
    [ Terminal_Ready_For_CVM_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        pubkBank, 'Null', IAD, AC, 'ARQC', ATC, CTQ) ]

rule Terminal_Receives_Records_DDA_Visa:
    let PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
        AIP = <furtherData, 'DDA'>
        d = <format, ATC, ~UN, $amount, 'CHF', nc, CTQ, AIP>
        records = <~PAN, expDate, $CA,
                    <<'02', $Bank, pubkBank, $CA>, sign2>,
                    <<'04', ~PAN, pubkCard, $Bank>, sign3>,
                    nc, CTQ, SDAD>
    in
    [ Terminal_Sent_ReadRecord_Visa($Terminal, PDOL, AIP, DH, ~PAN,
        expDate, IAD, AC, CID, ATC, CTQ),
      In(records),
      !IssuingCA($Bank, $CA),
      !SDADFormat(format, CID),
      !CertCA($CA, <<'01', $CA, pubkCA, $CA>, sign1>) ]
  --[
      Eq( verify(sign1, <'01', $CA, pubkCA, $CA>, pubkCA), true ),
      Eq( verify(sign2, <'02', $Bank, pubkBank, $CA>, pubkCA), true ),
      Eq( verify(sign3, <'04', ~PAN, pubkCard, $Bank>, pubkBank), true ),
      Eq( verify(SDAD, d, pubkCard), true )
    ]->
    [ Terminal_Ready_For_CVM_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        pubkBank, 'Null', IAD, AC, CID, ATC, CTQ) ]

rule Terminal_Processes_CVM_NoPIN_Visa:
    let PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
    in
    [ Terminal_Ready_For_CVM_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        pubkBank, nc, IAD, AC, CID, ATC, 'NoPIN'),
      !Value($amount, 'Low') ]
  -->
    [ Terminal_Ready_To_Send_AC_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        nc, IAD, AC, CID, ATC, 'NoPIN', 'Null') ]

rule Terminal_Processes_CVM_CDCVM_Visa:
    [ Terminal_Ready_For_CVM_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        pubkBank, nc, IAD, AC, 'ARQC', ATC, 'CDCVM') ]
  -->
    [ Terminal_Ready_To_Send_AC_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        nc, IAD, AC, 'ARQC', ATC, 'CDCVM', 'Null') ]

rule Terminal_Processes_CVM_OnlinePIN_Visa:
    let PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
    in
    [ Terminal_Ready_For_CVM_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        pubkBank, nc, IAD, AC, 'ARQC', ATC, 'OnlinePIN'),
      !Entered_PIN(~PAN, PIN),
      !Value($amount, 'High') ]
  -->
    [ Terminal_Ready_To_Send_AC_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        nc, IAD, AC, 'ARQC', ATC, 'OnlinePIN', aenc{PIN}pubkBank) ]

rule Terminal_Sends_AC_Visa:
    let PDOL = <TTQ, $amount, country, currency, date, type, ~UN>
        CDOL1 = <'TVR', 'NoPIN', 'HHMMSS'>
        X = <<$amount, country, currency, date, type, ~UN>, CDOL1>
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, 'Visa'>
    in
    [ Terminal_Ready_To_Send_AC_Visa($Terminal, ~PAN, PDOL, AIP, DH, $CA, $Bank,
        nc, IAD, AC, CID, ATC, CVM, encPIN),
      Fr(~channelID) ]
  --[ Running($Terminal, $Bank, <'Terminal', 'Bank', transaction>) ]->
    [ Terminal_Received_AC_Visa($Terminal, $Bank, $CA,
        nc, CID, transaction, ~channelID),
      Send($Terminal, $Bank, <~channelID, '1'>, <transaction, encPIN>)]
*/

rule Bank_Receives_AC:
    let AC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
        ARPC = MAC_arpc(f(~MK, ATC), AC XOR p8('ARC'))
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, AID>
    in
    [ Recv($Terminal, $Bank, <channelID, '1'>,
        <transaction, encPIN>),
      !Shk(~PAN, ~MK),
      !IssuingBank(~PAN, $Bank) ]
  --[ Once(<~PAN, ATC, 'Bank'>) ]->
    [ Bank_Checked_AC($Bank, $Terminal, transaction, encPIN, channelID, ARPC) ]

rule Bank_Receives_AC_Failed:
    let correctAC = MAC(f(~MK, ATC), <X, AIP, ATC, IAD>)
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, AID>
    in
    [ Recv($Terminal, $Bank, <channelID, '1'>,
        <transaction, encPIN>),
      !Shk(~PAN, ~MK) ]
  --[ NEq(correctAC, AC),
      BankDeclines(transaction) ]->
    [ ]

rule Bank_Processes_CVM_NotOnlinePIN:
    let transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, AID>
    in
    [ Bank_Checked_AC($Bank, $Terminal, transaction, 'Null', channelID, ARPC) ]
  --[ NEq(CVM, 'OnlinePIN'),
      Running($Bank, $Terminal, <'Bank', 'Terminal', transaction>) ]->
    [ Bank_Commits($Bank, $Terminal, transaction, channelID, ARPC) ]

rule Bank_Commits:
    let PDOL = <amount, country, currency, date, type, UN>
        X = <PDOL, CDOL1>
        AIP = <'CDA', furtherData>
        AID2 = 'Mastercard'
        value = 'High'
        transaction = <~PAN, AIP, CVM, X, ATC, DH, AC, IAD, AID>
    in
    [ Bank_Commits($Bank, $Terminal, transaction, channelID, ARPC),
      !Value(amount, value),
      !AID(~PAN, AID2),
      !IssuingCA($Bank, $CA) ]
  --[ Commit($Bank, ~PAN, <'Card', 'Bank', transaction>),
      Commit($Bank, $Terminal, <'Terminal', 'Bank', transaction>),
      Honest($CA), Honest($Bank), Honest($Terminal), Honest(~PAN) ]->
    [ Send($Bank, $Terminal, <channelID, '2'>, <'ARC', ARPC>) ]

rule Bank_Processes_CVM_OnlinePIN:
    let encPIN = aenc{PIN}pk(~privkBank)
        transaction = <~PAN, AIP, 'OnlinePIN', X, ATC, DH, AC, IAD, AID>
    in
    [ Bank_Checked_AC($Bank, $Terminal, transaction, encPIN, channelID, ARPC),
      !LtkBank($Bank, ~privkBank),
      !PIN(~PAN, PIN) ]
  --[ Running($Bank, $Terminal, <'Bank', 'Terminal', transaction>) ]->
    [ Bank_Commits($Bank, $Terminal, transaction, channelID, ARPC) ]

restriction equal:
  "All a b #i. Eq(a, b)@i ==> a = b"

restriction not_equal:
  "All a #i. NEq(a, a)@i ==> F"

restriction once:
  "All a #i #j. Once(a)@i & Once(a)@j ==> #i = #j"

restriction unique_role:
  "All A r1 r2 #i #j. Role(A, r1)@i & Role(A, r2)@j ==> r1 = r2"

restriction compatibility:
  "
  (All #i. Compatible_CID_CVM('TC', 'OnlinePIN')@i ==> F) &
  (All #i. Compatible_CID_acType('TC', 'ARQC')@i ==> F)"

lemma executable:
  exists-trace
  "Ex Bank PAN t #i #j #k #l.
    i < j &
    Running(PAN, 'Terminal', <'Card', 'Terminal', t>)@i &
    Commit('Terminal', PAN, <'Card', 'Terminal', t>)@j &
    k < l &
    Running(PAN, Bank, <'Card', 'Bank', t>)@k &
    Commit(Bank, PAN, <'Card', 'Bank', t>)@l &
    (All #a #b. OneCard()@a & OneCard()@b ==> #a = #b) &
    (All #a #b. OneTerminal()@a & OneTerminal()@b ==> #a = #b) &
    (All A B r #a #b. Role(A, r)@a & Role(B, r)@b ==> A = B) &
    not (Ex A #a. Compromise(A)@a)"

lemma bank_accepts:
  "All t #i.
    TerminalAccepts(t)@i
   ==>
    not (Ex #j. BankDeclines(t)@j) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_terminal_minimal:
  "All T P r t #i.
    (All #a #b. OneCard()@a & OneCard()@b ==> #a = #b) &
    Commit(T, P, <r, 'Terminal', t>)@i
   ==>
    (Ex #j. Running(P, T, <r, 'Terminal', t>)@j) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_terminal:
  "All T P r t #i.
    Commit(T, P, <r, 'Terminal', t>)@i
   ==>
    ((Ex #j. Running(P, T, <r, 'Terminal', t>)@j & j < i) &
      not (Ex T2 P2 #i2. Commit(T2, P2, <r, 'Terminal', t>)@i2 & not(#i2 = #i))
    ) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_terminal_minimal_dh:
  "All T P DH #i.
    (All #a #b. OneCard()@a & OneCard()@b ==> #a = #b) &
    Commit_DH(T, P, DH)@i
   ==>
    (Ex #j. Running_DH(P, T, DH)@j) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_terminal_dh:
  "All T P DH #i.
    Commit_DH(T, P, DH)@i
   ==>
    ((Ex #j. Running_DH(P, T, DH)@j & j < i) &
      not (Ex T2 P2 #i2. Commit_DH(T2, P2, DH)@i2 & not(#i2 = #i))
    ) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_bank_minimal:
  "All B P r t #i.
    (All #a #b. OneCard()@a & OneCard()@b ==> #a = #b) &
    Commit(B, P, <r, 'Bank', t>)@i
   ==>
    (Ex #j. Running(P, B, <r, 'Bank', t>)@j) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma auth_to_bank:
  "All B P r t #i.
    Commit(B, P, <r, 'Bank', t>)@i
   ==>
    ((Ex #j. Running(P, B, <r, 'Bank', t>)@j & j < i) &
      not (Ex B2 P2 #i2. Commit(B2, P2, <r, 'Bank', t>)@i2 & not(#i2 = #i))
    ) |
    Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma secrecy_MK:
  "All MK #i.
    SecretMK(MK)@i
   ==>
    not (Ex #j. !KU(MK)@j) | Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma secrecy_privkCard:
  "All privkCard #i.
    SecretPrivkCard(privkCard)@i
   ==>
    not (Ex #j. !KU(privkCard)@j) | Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma secrecy_PIN:
  "All PIN #i.
    SecretPIN(PIN)@i
   ==>
    not (Ex #j. !KU(PIN)@j) | Ex A #k. Honest(A)@i & Compromise(A)@k"

lemma secrecy_PAN:
  "All PAN #i.
    SecretPAN(PAN)@i
   ==>
    not (Ex #j. !KU(PAN)@j) | Ex A #k. Honest(A)@i & Compromise(A)@k"
  
lemma secrecy_SIGMA:
  "All S #i.
    Secret_Sigma(S)@i
   ==>
    not (Ex #j. !KU(S)@j) | Ex A #k. Honest(A)@i & Compromise(A)@k"
end

